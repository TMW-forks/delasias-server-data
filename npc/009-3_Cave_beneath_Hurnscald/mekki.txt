// Assassinen Quest
// by Hawkmoon and Delasia @ irc.freenode.net
// 2010
// This is a fun search and find quest - but still high level, because the hiding areas are not good for low level chars.


009-3.gat,157,122,0	script	Mekki Da Boss	125,{
	mes "[Mekki]";
	mes "\"Hey man!\"";
	next;
	
	if (state == 0) goto L_Start;
	if (state & (1 << 10)) goto L_Finished;
	
	mes "[Mekki Da Boss]";
	mes "\"Wassup?\"";
	next;
	menu "Hey Boss, did like you asked! Found all yer boys!", L_Check, "Just checking in, Yer boys are good at hiding.", -, "Errr- what did ya want again?", L_Explain;
	mes "[Mekki Da Boss]";
	mes "\"Wouldn't be one of the boys if they weren't.";
	mes "Hurry up and find them, I need 'em here!\"";
	close;
	
L_Check:
	if (state & 0x3FF == 0x3FF) goto L_Complete;
	mes "[Mekki Da Boss]";
	mes "\"Ya lied to da Boss? Never lie to da Boss!\"";
	close;
	
L_Complete:
	mes "[Mekki Da Boss]";
	mes "\"Yo man!";
	mes "Knew I could count on ya!\"";
	next;
	mes "[Mekki Da Boss]";
	mes "\"Ya did a good job.\"";
	next;
	mes "Mekki stops for a moment and thinks.....";
	set state, state | (1 << 10);
	next;
	mes "[Mekki Da Boss]";
	mes "\"Ya know, the boys and I always appreciate GOOD Help";
	mes "And you did Good!\"";
	next;
	mes "[Mekki Da Boss]";
	mes "\"If you gonna play with the boys, ya gotta look like the boys";
	mes "We will call on you later!\"";
	mes "[100 000 experience points!]";
	mes "[100 000 gp!]";
	getexp 100000, 0;
	set zeny, zeny + 100000;
	getitem 731,1;
	getitem 755,1;
	getitem 757,1;
	getitem 756,1;
	mes "\"And wear your brotherhood gear with pride, man!\"";
	close;
	close;
	
L_Explain:
	mes "[Mekki Da Boss]";
	mes "\"Well you agreed to finding my boys, and giving them a message.";
	mes "Man, you better hurry, I need my boys, and your wasting time!\"";
	close;


L_Start:
	mes "[Mekki Da Boss]";
	mes "\"Hey who are you? You want something from me?";
	mes "You a cop? What do you want!\"";
	next;
	mes "[Mekki Da Boss]";
	mes "\"Lemme see your passport...I gotta be sure who I'm dealing with.";
	mes "";
	if (BaseLevel < 71) goto L_Young;
	mes "A dudes gotta be sure, ya know what I mean...Hey maybe you can help me?\"";
	next;
	menu "I can try, what ya need Mekki!", L_StartYes, "Sorry, I dont deal with YOUR type.", -;
	close;

L_Young:
	mes "\"You is too young to be here...come back when you is older?\"";
	close;

L_StartYes:
	set state, state | 1;
	mes "[Mekki Da Boss]";
	mes "\"I need you to find da Boys...Tell 'em I need all NINE of 'em, we have a job!";
	mes "Thanks man, I owe you.\"";
	next;
	mes "Mekki gives a HIGH 5.";
	close;

	
L_Finished:
	set @TEMP,rand(2);
	if(@TEMP == 0) goto L_Chat1;
	if(@TEMP == 1) goto L_Chat2;
	
L_Chat1:
	mes "[Mekki Da Boss]";
	mes "\"Ahh, that's so cool, man! Gonna have to tell the boys about it now!\"";
	close;
	
L_Chat2:
	mes "[Mekki Da Boss]";
	mes "\"Did I remember to get Mrs. Mekki something?";
	mes "Sh..., can't remember if I did.\"";
	close;
}

